plugins {
	id 'net.fabricmc.fabric-loom-remap' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	mavenCentral()
	exclusiveContent {
		forRepository {
			maven {
				name = "CurseMaven"
				url = "https://cursemaven.com"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

	// VulkanMod (required at runtime)
	modImplementation "curse.maven:vulkanmod-635429:${project.vulkanmod_file_id}"

	// LWJGL Vulkan bindings (missing from vanilla MC, required by VulkanMod)
	compileOnly "org.lwjgl:lwjgl-vulkan:${project.lwjgl_version}"
	runtimeOnly "org.lwjgl:lwjgl-vulkan:${project.lwjgl_version}"

	// LWJGL VMA bindings (required by VulkanMod)
	runtimeOnly "org.lwjgl:lwjgl-vma:${project.lwjgl_version}"
	runtimeOnly "org.lwjgl:lwjgl-vma:${project.lwjgl_version}:${project.lwjgl_natives}"

	// LWJGL Shaderc bindings (required by VulkanMod)
	runtimeOnly "org.lwjgl:lwjgl-shaderc:${project.lwjgl_version}"
	runtimeOnly "org.lwjgl:lwjgl-shaderc:${project.lwjgl_version}:${project.lwjgl_natives}"

	// TOML parsing for shaderpack metadata
	implementation "org.tomlj:tomlj:${project.tomlj_version}"
	include "org.tomlj:tomlj:${project.tomlj_version}"
	
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
